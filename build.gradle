plugins {
    id 'java'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.0.1'
    id 'com.enonic.xp.base' version '2.0.0'
    id "com.github.node-gradle.node" version '2.2.3'
}

repositories {
    jcenter()
    xp.enonicRepo()
}


group = 'com.enonic.lib'

jar {
    archiveFileName = 'lib-admin-ui'
}

node {
    download = true
    version = '14.15.0'
}

def resourcesDir = file( 'src/main/resources' )

loadConfiguration()

task lint( type: NpmTask, dependsOn: npmInstall ) {
    args = [ 'run', 'lint' ]
    inputs.files fileTree( dir: 'modules', include: '**/src/main/**.*' )
    outputs.dir file('gradle')
    outputs.upToDateWhen { false }
}

task typescriptPkg( type: NpmTask, dependsOn: lint ) {
    def buildEnv = isProd() ? 'prod' : 'dev'
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    args = [ 'run', "build:$buildEnv" ]
    inputs.dir "${resourcesDir}/assets/admin/common"
    outputs.dir "${buildDir}/resources/main/dev/lib-admin-ui"
}

task typescriptLib( type: NpmTask, dependsOn: lint ) {
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    description = 'Build library, that should be included to HTML.'
    args = [ 'run', 'build:lib' ]
    inputs.dir "${resourcesDir}/assets/admin/common"
    outputs.dir "${buildDir}/resources/main/assets/admin/common"
}

task minifyPkg( type: NpmTask, dependsOn: typescriptPkg ) {
    description = 'Minify transpiled code.'
    args = [ 'run', "build:minify" ]
    inputs.dir "${buildDir}/resources/main/dev/lib-admin-ui"
    outputs.dir "${buildDir}/resources/main/dev/lib-admin-ui"
    mustRunAfter typescriptPkg
    enabled isProd()
}

task copyPackage( type: Copy ) {
    from( './' ) {
        include 'package.json'
    }
    into "$buildDir/resources/main/dev/lib-admin-ui"
    includeEmptyDirs = false
}

task typescript {
    dependsOn 'typescriptPkg'
    dependsOn 'typescriptLib'
    dependsOn 'minifyPkg'
    dependsOn 'copyPackage'
}

jar {
    exclude 'dev/lib-admin-ui/lib.js'
    exclude 'assets/spec/**'
    exclude 'assets/**/*.ts'
    exclude 'assets/**/*.less'
    exclude 'assets/**/styles/*.js'
    exclude 'assets/**/icons/*.txt'
    exclude 'assets/**/icons/**/icomoon.*'

    // COMMON/LIB excludes
    // Exclude complex files, that only present after the first build and must be evaluated dynamically
    def whitelist = [
        /.*lib\.js$/,
        /.*\.css$/,
    ] as List

    if (isProd()) {
        exclude '**/*.map'
    } else {
        whitelist.add( /.*\.map$/ )
    }

    exclude {
        FileTreeElement el -> !el.directory && el.relativePath.contains('common/lib') && (!whitelist.any { re -> el.relativePath.toString().matches( re ) })
    }

    includeEmptyDirs = false
    dependsOn += typescript
}

task npmInstallForce( type: NpmTask ) {
    description = 'Update all project node dependencies'
    args = [ 'install', '--force' ]
}

task cleanNode( type: Delete ) {
    delete += 'node_modules'
}

task flush( type: Delete, dependsOn: clean ) {
    description = 'Clean the project from built sources and dependencies'
    delete '.xp'
}

def loadConfiguration() {
    def envList = [ 'dev', 'prod' ]
    def environment = hasProperty( 'env' ) ? env : 'prod'

    if ( !envList.contains( environment ) ) {
        def message = "Target environment '$environment' is not allowed. Please use one of the following: ${envList.join(", ")}"
        throw new ResourceException( message )
    }

    def isDev = environment != 'prod'
    project.ext.isDev = isDev

    def envName = nodeEnvironment()
    println "Environment is set to $envName"
}

def nodeEnvironment() {
    def environments = [ prod : 'production', dev: 'development' ]
    def nodeEnv = environments[hasProperty( 'env' ) ? env : 'prod']
    return nodeEnv != null ? nodeEnv : 'production'
}

def isProd() {
    return hasProperty( 'env' ) ? (env == 'prod') : true
}
