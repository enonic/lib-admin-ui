plugins {
    id 'java'
    id 'maven'
    id 'com.enonic.defaults' version '1.0.3'
    id 'com.enonic.xp.base' version '1.0.7'
    id 'com.moowork.gulp' version '1.2.0'
}

repositories {
    jcenter()
    xp.enonicRepo()
}


group = 'com.enonic.lib'
version = '1.0.2'

jar {
    baseName = 'lib-admin-ui'
}

node {
    download = true
    version = '6.9.2'
}

ext {
    resourcesDir = file( 'src/main/resources' )
}

task gulpAll( type: GulpTask, dependsOn: npmInstall ) {
    description = 'Build UI resources (css, etc).'
    inputs.files fileTree( dir: 'src/main/resources', exclude: '**/_all.*' )
    outputs.files fileTree( dir: 'src/main/resources', include: '**/_all.*' )
    args = ['all']
}

task copyTypeDefs( type: Copy, dependsOn: gulpAll ) {
    description = 'Copy Typescript definitions to /dev/d.ts folder.'
    from( 'src/main/resources/assets/admin/common/js' ) {
        include '**/*.d.ts'
        rename '_all.d.ts', 'admin.d.ts'
    }
    into 'build/resources/main/dev/d.ts'
    includeEmptyDirs = false
}

task copySharedStyles( type: Copy, dependsOn: copyTypeDefs ) {
    description = 'Copy shared styles to /dev/styles folder.'
    from( 'src/main/resources/assets/admin/common/styles' ) {
        include '**/*.less'
        into 'styles'
    }
    from( 'src/main/resources/assets/admin/common/images' ) {
        into 'images'
    }
    from( 'src/main/resources/assets/admin/common/font' ) {
        into 'font'
    }
    from( 'src/main/resources/assets/admin/common/icons' ) {
        into 'icons'
    }
    includeEmptyDirs = false
    into 'build/resources/main/dev'
}

jar {
    exclude 'assets/spec/**'
    exclude 'assets/**/*.ts'    // let dev/**/admin.d.ts through to be used by other apps
    exclude 'assets/**/*.less'  // let dev/**/*.less through to be used by other apps
    includeEmptyDirs = false
    dependsOn += copySharedStyles
}

task cleanNode( type: Delete ) {
    delete += 'node_modules'
}
